plugins {
    id "io.swagger.core.v3.swagger-gradle-plugin" version "2.0.5"
}

repositories {
    maven {
        url 'http://dynamodb-local.s3-website-us-west-2.amazonaws.com/release'
    }

    maven { url "https://dl.bintray.com/orestis-unit/aws-build-tools" }
}

apply plugin: 'java'
apply plugin: "io.swagger.core.v3.swagger-gradle-plugin"

dependencies {

    implementation project(":dynamo")

    //jersey libraries
    implementation("org.glassfish.jersey.media:jersey-media-json-jackson:2.27")
    implementation("org.glassfish.jersey.inject:jersey-hk2:2.27") {
        exclude group: 'javax.inject'
    }

    // swagger
    implementation('io.swagger.core.v3:swagger-jaxrs2:2.0.0')
    implementation('io.swagger.core.v3:swagger-annotations:2.0.0')
    implementation('io.swagger.core.v3:swagger-models:2.0.0')

    //connection to amazon
    implementation("com.amazonaws:aws-lambda-java-core:1.2.0")
    implementation("com.amazonaws.serverless:aws-serverless-java-container-jersey:1.1")

    implementation("no.bibsys.aws:aws-build-tools:1.0.2")

    // Handlebars
    implementation("com.github.jknack:handlebars:4.1.2")


    // for testing
    testImplementation ("com.amazonaws:DynamoDBLocal:1.11.+")
    testRuntimeClasspath ("com.amazonaws:DynamoDBLocal:1.11.+")
    testImplementation("org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:2.27")
    testImplementation("org.hamcrest:hamcrest-junit:2.0.0.0")
    testImplementation("org.mockito:mockito-core:2.23.0")
}



task copyNativeDeps(type: Copy) {
    from(configurations.testRuntimeClasspath) {
        include '*.dll'
        include '*.dylib'
        include '*.so'
    }
    into 'build/libs'
}


test {
    dependsOn copyNativeDeps
    //Copy native path for testing locally DynamoDB
    systemProperty "java.library.path", 'build/libs'
}


jar.enabled = false


shadowJar {
    archiveName = "${baseName}-fat.jar"
    exclude('org.apache.tomcat.embed:*')
}

resolve {
    outputFormat = 'YAML'
    prettyPrint = 'TRUE'
    classpath = sourceSets.main.runtimeClasspath
    resourcePackages = ['no.bibsys.web']
    outputPath = "${buildDir}/openapi"
}